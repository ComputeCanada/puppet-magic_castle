class profile::cvmfs::client (
  Integer $quota_limit,
  Variant[Boolean, String] $disable_autofs = false,
  Boolean $strict_mount = false,
  Array[String] $repositories = [],
  Array[String] $alien_cache_repositories = [],
  String $cvmfs_root = '/cvmfs',
) {
  include profile::consul
  include profile::cvmfs::local_user
  $alien_fs_root_raw = lookup('profile::cvmfs::alien_cache::alien_fs_root', undef, undef, 'scratch')
  $alien_fs_root = regsubst($alien_fs_root_raw, '^/|/$', '', 'G')
  $alien_folder_name_raw = lookup('profile::cvmfs::alien_cache::alien_folder_name', undef, undef, 'cvmfs_alien_cache')
  $alien_folder_name = regsubst($alien_folder_name_raw, '^/|/$', '', 'G')

  yumrepo { 'cvmfs-repo':
    enabled             => true,
    descr               => 'CernVM packages',
    baseurl             => 'http://cvmrepo.s3.cern.ch/cvmrepo/yum/cvmfs/EL/$releasever/$basearch/ http://cvmrepo.web.cern.ch/cvmrepo/yum/cvmfs/EL/$releasever/$basearch/',
    skip_if_unavailable => true,
    gpgcheck            => 1,
    gpgkey              => 'http://cvmrepo.s3.cern.ch/cvmrepo/RPM-GPG-KEY-CernVM-2048',
    repo_gpgcheck       => 1,
    protect             => 1,
  }

  package { 'cvmfs':
    ensure  => 'installed',
    require => [Yumrepo['cvmfs-repo']],
  }

  file { $cvmfs_root:
    ensure  => directory,
    seltype => 'root_t',
  }

  file_line { 'cvmfs_default':
    path    => '/etc/cvmfs/default.conf',
    match   => '^CVMFS_HTTP_PROXY',
    line    => 'CVMFS_HTTP_PROXY=DIRECT',
    require => Package['cvmfs'],
  }

  file_line { 'cvmfs_mount_dir':
    ensure  => present,
    path    => '/etc/cvmfs/default.conf',
    line    => "  readonly CVMFS_MOUNT_DIR=${cvmfs_root}",
    match   => '^  readonly CVMFS_MOUNT_DIR=/cvmfs$',
    require => Package['cvmfs']
  }

  file { '/etc/cvmfs/default.local.ctmpl':
    content => epp('profile/cvmfs/default.local', {
        'strict_mount' => $strict_mount ? { true => 'yes', false => 'no' }, # lint:ignore:selector_inside_resource
        'quota_limit'  => $quota_limit,
        'repositories' => $repositories + $alien_cache_repositories,
    }),
    notify  => Service['consul-template'],
    require => Package['cvmfs'], # 'cvmfs' packages provides /etc/cvmfs
  }

  $alien_cache_repositories.each |$repo| {
    file { "/etc/cvmfs/config.d/${repo}.conf":
      content => epp('profile/cvmfs/alien_cache.conf.epp', {
          'alien_fs_root'     => $alien_fs_root,
          'alien_folder_name' => $alien_folder_name,
      }),
      require => Package['cvmfs'], # 'cvmfs' packages provides /etc/cvmfs/config.d
    }
  }

  consul_template::watch { '/etc/cvmfs/default.local':
    require     => File['/etc/cvmfs/default.local.ctmpl'],
    config_hash => {
      perms       => '0644',
      source      => '/etc/cvmfs/default.local.ctmpl',
      destination => '/etc/cvmfs/default.local',
      command     => '/usr/bin/cvmfs_config reload',
    },
  }

  if Boolean($disable_autofs) {
    $repositories.each|$repository| {
      file { "/cvmfs/${repository}":
        ensure  => directory,
        require => File[$cvmfs_root],
      }
      -> mount { "/cvmfs/${repository}":
        ensure  => 'mounted',
        device  => $repository,
        fstype  => 'cvmfs',
        require => [
          Package['cvmfs'],
          File_line['cvmfs_default'],
        ],
      }
      Package<| tag == profile::software_stack |> -> Mount["/cvmfs/${repository}"]
    }
  } else {
    file { '/etc/auto.master.d/cvmfs.autofs':
      notify  => Service['autofs'],
      require => [
        Package['cvmfs'],
        File['/cvmfs'],
      ],
      content => @("EOF")
        # generated by Puppet for CernVM-FS
        ${cvmfs_root} /etc/auto.cvmfs
        |EOF
    }

    service { 'autofs':
      ensure => running,
      enable => true,
    }

    # Make sure CVMFS repos are mounted when requiring this class
    exec { 'init_default.local':
      command     => 'consul-template -template="/etc/cvmfs/default.local.ctmpl:/etc/cvmfs/default.local" -once',
      environment => ["CONSUL_TOKEN=${lookup('profile::consul::acl_api_token')}"],
      path        => ['/bin', '/usr/bin', $consul_template::bin_dir],
      unless      => 'test -f /etc/cvmfs/default.local',
      require     => [
        File['/etc/cvmfs/default.local.ctmpl'],
        Service['consul'],
        Service['autofs'],
      ],
    }
    Package<| tag == profile::software_stack |> ~> Service['autofs']
  }

  # Fix issue with BASH_ENV, SSH and lmod where
  # ssh client would get a "Permission denied" when
  # trying to connect to a server. The errors
  # results from the SELinux context type of
  # /cvmfs/soft.computecanada.ca/nix/var/nix/profiles/16.09/lmod/lmod/init/bash
  # To be authorized in the ssh context, it would need
  # to be a bin_t type, but it is a fusefs_t and since
  # CVMFS is a read-only filesystem, the context cannot be changed.
  # 'use_fusefs_home_dirs' policy fix that issue.
  selinux::boolean { 'use_fusefs_home_dirs': }
}

# Create an alien source that refers to the uid and gid of cvmfs user
class profile::cvmfs::alien_cache (
  String $alien_fs_root_raw = 'scratch',
  String $alien_folder_name_raw = 'cvmfs_alien_cache',
) {
  $uid = lookup('profile::cvmfs::local_user::uid', undef, undef, 13000004)
  $gid = lookup('profile::cvmfs::local_user::gid', undef, undef, 8000131)
  $alien_fs_root = regsubst($alien_fs_root_raw, '^/|/$', '', 'G')
  $alien_folder_name = regsubst($alien_folder_name_raw, '^/|/$', '', 'G')

  # Ensure the alien cache parent folder exists
  ensure_resource('file', "/mnt/${alien_fs_root}", { 'ensure' => 'directory', 'seltype' => 'home_root_t' })

  file { "/mnt/${alien_fs_root}/${alien_folder_name}":
    ensure  => directory,
    group   => $gid,
    owner   => $uid,
    require => File["/mnt/${alien_fs_root}"],
    seluser => 'unconfined_u',
  }
}

# Create a local cvmfs user
class profile::cvmfs::local_user (
  String $uname = 'cvmfs',
  String $group = 'cvmfs-reserved',
  Integer $uid = 13000004,
  Integer $gid = 8000131,
  String $selinux_user = 'unconfined_u',
  String $mls_range = 's0-s0:c0.c1023',
) {
  group { $group:
    ensure => present,
    gid    => $gid,
    before => Package['cvmfs'],
  }
  user { $uname:
    ensure     => present,
    forcelocal => true,
    uid        => $uid,
    gid        => $gid,
    managehome => false,
    home       => '/var/lib/cvmfs',
    shell      => '/usr/sbin/nologin',
    require    => Group[$group],
    before     => Package['cvmfs'],
  }
  if $group != 'cvmfs' {
    # cvmfs rpm create a user and a group 'cvmfs' if they do not exist.
    # If the group created for the local user 'cvmfs' is not named 'cvmfs',
    # we make sure the group 'cvmfs' is attributed the same gid before installing
    # package cvmfs.
    group { 'cvmfs':
      allowdupe => true,
      gid       => $gid,
      require   => Group[$group],
      before    => Package['cvmfs'],
    }
  }
}
